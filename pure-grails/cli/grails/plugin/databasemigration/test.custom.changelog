import liquibase.statement.core.AddColumnStatement
import liquibase.statement.core.DropColumnStatement

databaseChangeLog = {
	changeSet(author: 'burt', id: 'create_grails_table') {

		createTable(tableName: 'grails') {
			column(autoIncrement: 'true', name: 'ID', type: 'BIGINT') {
				constraints(nullable: 'false', primaryKey: 'true', primaryKeyName: 'pk_grails')
			}

			column(name: 'name', type: 'varchar(255)') {
				constraints(nullable: 'false')
			}

			column(name: 'foo', type: 'bigint') {
				constraints(nullable: 'false')
			}
		}
	}

	changeSet(author: 'burt', id: 'customChanges') {

		grailsChange {

			priority 123

			init {
				new File('target/created_in_init').withWriter {
					it.write "in init\n"
					it.write "database: $database\n"
					it.write "databaseConnection: $databaseConnection\n"
					it.write "connection: $connection\n"
				}
			}

			validate {
				warn 'Keep out of reach of children'
			}

			change {
				new File('target/created_in_create').withWriter {
					it.write "in create\n"
					it.write "database class: '${database.getClass().name}', typeName: '$database.typeName'\n"
					it.write "databaseConnection class: '${databaseConnection.getClass().name}'\n"
					it.write "connection class: '${connection.getClass().name}'\n"
				}

				sqlStatement new AddColumnStatement(null, 'grails', 'bar', 'bigint', null)

				20.times { i ->
					sql.executeInsert("insert into grails(name, foo) values (?, ?)",
						['name' + i, i + 100])
				}

				confirm 'Change is good'
			}

			rollback {
				new File('target/created_in_rollback').withWriter {
					it.write "in rollback\n"
					it.write "database class: '${database.getClass().name}', typeName: '$database.typeName'\n"
					it.write "databaseConnection class: '${databaseConnection.getClass().name}'\n"
					it.write "connection class: '${connection.getClass().name}'\n"
				}

				sql.executeUpdate "delete from grails where foo>=100 and foo<=121"

				sqlStatements([new DropColumnStatement(null, 'grails', 'bar')])

				confirm 'Rolled back'
			}

			checkSum 'check_sum_value'
		}
	}
}
